apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.novoda.bintray-release'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
  testCompile "org.mockito:mockito-core:${versions.mockito}"
  testCompile "junit:junit:${versions.junit}"
}

jacocoTestReport {
  reports {
    xml.enabled true
    csv.enabled false
  }
}

publish {
  groupId = 'com.github.amatkivskiy'
  artifactId = 'result'
  publishVersion = VERSION_NAME
  desc = 'Simple and lightweight library that implements Railway Oriented Programming approach for error handling.'
  licences = ['Apache-2.0']
  website = 'https://github.com/amatkivskiy/ResultForJava'
  autoPublish = true
  bintrayUser = 'amatkivskiy'
  bintrayKey = System.getenv("BINTRAY_API_KEY") ?: ""
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
  archives sourcesJar, javadocJar
}

uploadArchives {
  repositories {
    flatDir {
      dirs '../artifacts'
    }
  }
}